plugins {
    id 'application'
    id 'java'
    id 'jacoco'
}

group 'uk.ashleybye'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
}

run {
    mainClassName = 'uk.ashleybye.tictactoe.TicTacToe'
    standardInput = System.in
}

jar {
    manifest {
        attributes(
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Main-Class': 'uk.ashleybye.tictactoe.TicTacToe'
        )
    }
    destinationDir = file("$rootDir")
}

wrapper {
    gradleVersion = '4.9'
}

clean {
    delete "${rootDir}/out"
    delete "${rootDir}/${rootProject.name}.jar"
}

test { useJUnitPlatform() }

jacoco {
    toolVersion = "0.8.1"
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled true
        html.enabled true
    }
}

jacocoTestCoverageVerification {
    violationRules.rule {
        limit { counter = 'INSTRUCTION'; minimum = 0.95 }
    }
}

check.dependsOn jacocoTestReport
check.dependsOn jacocoTestCoverageVerification

